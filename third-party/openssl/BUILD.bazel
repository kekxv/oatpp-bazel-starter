"""An openssl build file based on a snippet found in the github issue:
https://github.com/bazelbuild/rules_foreign_cc/issues/337
"""

load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

exports_files(["repositories.MODULE.bazel"])

# Read https://wiki.openssl.org/index.php/Compilation_and_Installation

CONFIGURE_OPTIONS = [
    "no-shared",
    "no-sse2",
    "-static",
]

configure_make(
    name = "openssl-build",
    args = ["-j4"],
    configure_command = "Configure",
    configure_options = CONFIGURE_OPTIONS + select({
        "@platforms//os:osx": [
            "ARFLAGS=\"-static -o\"",
        ],
        "//conditions:default": [
            "-fPIC",
        ],
    }),
    env = select({
        "@platforms//os:linux": {
            "CFLAGS": "-fPIC",
            "CPPFLAGS": "-fPIC",
        },
        "//conditions:default": {},
    }),
    lib_source = "@openssl//:all",
    linkopts = select({
        "//conditions:default": [
        ],
    }),
    out_lib_dir = select({
        "@platforms//os:linux": "lib64",
        "//conditions:default": "lib",
    }),
    out_static_libs = select({
        "@platforms//os:windows": [
            "libssl.lib",
            "libcrypto.lib",
        ],
        "//conditions:default": [
            "libssl.a",
            "libcrypto.a",
        ],
    }),
    target_compatible_with = select({
        "//conditions:default": [],
    }),
    targets = [
        "build_libs",
        "install_dev",
    ],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "openssl",
    visibility = ["//visibility:public"],
    deps = select({
        "//conditions:default": [
            ":openssl-build",
        ],
    }),
)
